apply plugin: 'com.jfrog.bintray'

version = '0.10.0' //YOUR LIBRARY VERSION

/*
 * Comment the following part if you only want to distribute .aar files.
 * (For example, your source code is obfuscated by Proguard and is not shown to outsite developers)
 * Without source code .jar files, you only can publish on bintray respository but not jcenter.
 */

/*--------------------------------*/
if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
/*--------------------------------*/

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "noke-mobile-library-android"
        desc = "An Android library used in conjunction with the Nok Core API to interact with Nok Devices"
        userOrg = "noke"   // If the repository is hosted by an organization instead of personal account.
        websiteUrl = "https://github.com/noke-inc/noke-mobile-library-android"
        vcsUrl = "https://github.com/noke-inc/noke-mobile-library-android"
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            desc = "An Android library used in conjunction with the Nok Core API to interact with Nok Devices"
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}